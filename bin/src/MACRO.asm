;公共宏

;AX,BX,CX,DX寄存器压栈
__PUSH_REGS MACRO
    PUSH AX
    PUSH BX
    PUSH CX
    PUSH DX
    ENDM
;AX,BX,CX,DX寄存器出栈，与__PUSH_REGS对应
__POP_REGS MACRO
    POP DX
    POP CX
    POP BX
    POP AX
    ENDM

;设置显示模式
;入口参数：显示模式MODE      出口参数：无
__SET_DISPLAY_MODE MACRO MODE
    PUSH AX
    MOV AH,00H
    MOV AL,MODE
    INT 10H
    ENDM

;设置0页光标位置
;入口参数：光标行列(ROW,COL)  出口参数：无
__SET_CURSOR MACRO ROW,COL
    PUSH AX
    PUSH BX
    PUSH DX
    MOV AH,02H
    MOV BH,0
    MOV DL,COL
    MOV DH,ROW
    INT 10H
    POP DX
    POP BX
    POP AX
    ENDM

;隐藏光标
__HIDE_CURSOR MACRO
    PUSH AX
    PUSH CX
    MOV CX,2000H  ;如果CH的第5位被置1(加20H),光标将不显示
    MOV AH,01H
    INT 10H
    POP CX
    POP AX
    ENDM

;读0页图形像素
;入口参数：坐标(X,Y)   出口参数：AL = 像素值
__READ_PIXEL MACRO X,Y
    PUSH BX
    PUSH CX
    PUSH DX
    MOV AH,0DH
    MOV BH,0
    MOV CX,X
    MOV DX,Y
    INT 10H
    POP DX
    POP CX
    POP BX
    ENDM

;写0页图形像素
;入口参数：坐标(X,Y) 像素值   出口参数：无
__WRITE_PIXEL MACRO X,Y,ATTR
    __PUSH_REGS
    MOV AH,0CH
	MOV AL,ATTR
    MOV BH,0
    MOV CX,X
    MOV DX,Y
    INT 10H
    __POP_REGS
    ENDM

;以字符行列方式给矩形框填充颜色
;入口参数：矩形框左上角位置(ROW1,COL1)
;          矩形框右下角位置(ROW2,COL2)
;          属性ATTR
;出口参数：无
__FILL MACRO ROW1,COL1,ROW2,COL2,ATTR
    __PUSH_REGS
    MOV AX,0600H
    MOV BH,ATTR
    MOV CH,ROW1
    MOV CL,COL1
    MOV DH,ROW2
    MOV DL,COL2
    INT 10H
    __POP_REGS
    ENDM

;封装显示字符串函数
__DISPLAY_STRING MACRO ROW,COL,STR_SEG,STR_ADDR,STR_LEN,STR_COLOR
	PUSH AX   ;保护AX
	MOV AX,ROW
	PUSH AX
	MOV AX,COL
	PUSH AX
	MOV AX,STR_SEG
	PUSH AX
	LEA AX,STR_ADDR
	;;;;;;;;;;;MOV AX,OFFSET STR_ADDR
	PUSH AX
	MOV AX,STR_LEN
	PUSH AX
	MOV AX,STR_COLOR
	PUSH AX
	CALL _DISPLAY_STRING
	ADD SP,12
	POP AX
	ENDM
;封装_SAVE_BLOCK函数
__SAVE_BLOCK MACRO X,Y,WIDTH_,HEIGHT,BUF
	PUSH AX
	MOV AX,X
	PUSH AX
	MOV AX,Y
	PUSH AX
	MOV AX,WIDTH_
	PUSH AX
	MOV AX,HEIGHT
	PUSH AX
	MOV AX,OFFSET BUF
	PUSH AX
	
	CALL _SAVE_BLOCK
	ADD SP,10
	POP AX
	ENDM
;封装_RESTORE_BLOCK函数
__RESTORE_BLOCK MACRO X,Y,WIDTH_,HEIGHT,BUF
	PUSH AX
	MOV AX,X
	PUSH AX
	MOV AX,Y
	PUSH AX
	MOV AX,WIDTH_
	PUSH AX
	MOV AX,HEIGHT
	PUSH AX
	MOV AX,OFFSET BUF
	PUSH AX
	
	CALL _RESTORE_BLOCK
	ADD SP,10
	POP AX
	ENDM
;封装_WRITE_BLOCK_PIXEL函数
__FILL_BY_PIXEL MACRO X,Y,WIDTH_,HEIGHT,ATTR
	PUSH AX
	MOV AX,X
	PUSH AX
	MOV AX,Y
	PUSH AX
	MOV AX,WIDTH_
	PUSH AX
	MOV AX,HEIGHT
	PUSH AX
	MOV AX,ATTR
	PUSH AX
	
	CALL _WRITE_BLOCK_PIXEL
	ADD SP,10
	POP AX
	ENDM

__DELAY MACRO COUNT    ;延时15.08毫秒 代码来自:P391 IBM-PC
	LOCAL LOOP1
	PUSH AX
	PUSH CX
	MOV CX,COUNT
LOOP1:
	IN AL,61H
	AND AL,10H
	CMP AL,AH
	JE LOOP1
	MOV AH,AL
	LOOP LOOP1
	POP CX
	POP AX
	ENDM

__CLEAR_INPUT_BUF MACRO     ;清除键盘缓冲区（读空）
	LOCAL NOT_EMPTY,EMPTY
	PUSH AX
NOT_EMPTY:
	MOV AH,01H
	INT 16H
	JZ EMPTY
	XOR AX,AX
	INT 16H
	JMP NOT_EMPTY
EMPTY:
	POP AX
ENDM

__CHANGE_DRAW_COLOR MACRO COLOR
	PUSH AX
	PUSH ES
	MOV AX,GLOBAL
	MOV ES,AX
	MOV AL,COLOR
	MOV ES:DRAW_COLOR,AL
	POP ES
	POP AX
	ENDM

__CHANGE_DRAW_TYPE MACRO TYPE
	PUSH AX
	PUSH ES
	MOV AX,GLOBAL
	MOV ES,AX
	MOV AL,TYPE
	MOV ES:DRAW_TYPE,TYPE
	POP ES
	POP AX
	ENDM
	
__DISPLAY_DRAW_TYPE MACRO
	LOCAL NEXT1,NEXT2,NEXT3,NEXT4,NEXT5
	PUSH AX
	PUSH BX
	PUSH ES
	MOV AX,GLOBAL
	MOV ES,AX
	
	MOV BH,0
	MOV BL,ES:DRAW_COLOR
	CMP ES:DRAW_TYPE,0
	JNZ NEXT1
	__DISPLAY_STRING 6,69,DATA,S_LINE,9,BX
NEXT1:
	CMP ES:DRAW_TYPE,1
	JNZ NEXT2
	__DISPLAY_STRING 6,69,DATA,S_POLYLINE,9,BX
NEXT2:
	CMP ES:DRAW_TYPE,2
	JNZ NEXT3
	__DISPLAY_STRING 6,69,DATA,S_CIRCLE,9,BX
NEXT3:
	CMP ES:DRAW_TYPE,3
	JNZ NEXT4
	__DISPLAY_STRING 6,69,DATA,S_ARC,9,BX
NEXT4:
	CMP ES:DRAW_TYPE,4
	JNZ NEXT5
	__DISPLAY_STRING 6,69,DATA,S_ELLIPSE,9,BX
NEXT5:
	POP ES
	POP BX
	POP AX
	ENDM
	
__DISPLAY_DRAW_COLOR MACRO
	PUSH AX
	PUSH ES
	MOV AX,GLOBAL
	MOV ES,AX
	
	__FILL 4,70,4,76,ES:DRAW_COLOR
	__FILL_BY_PIXEL 560,64,56,1,0FH
	__FILL_BY_PIXEL 560,80,56,1,0FH
	__FILL_BY_PIXEL 560,64,1,16,0FH
	__FILL_BY_PIXEL 616,64,1,16,0FH
	POP ES
	POP AX
	ENDM
